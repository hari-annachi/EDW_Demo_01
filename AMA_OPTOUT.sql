USE [EDW]
GO

/****** Object:  StoredProcedure [dbo].[spc_DIM_PRESCRIBER_AMA_OUTPUT_FLAG_UPPDATE]    Script Date: 4/6/2021 2:48:07 PM ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO



-- =============================================================================================
-- Author:		Vivek Palanisamy
-- Create date: 5/14/2020
-- Description:	Updates Edw.DIM_PRESCRIBER's AMA_OUTPUT_FLAG_KEY 
-- 11/26/2020    modified Vivek TP - Altered the data pull to the stage table STG_TORRO_PRESCRIBER
--                                 - and the target table to be DIM_MASTER_PRESCRIBER - STRY0087953
-- =============================================================================================

CREATE PROCEDURE [dbo].[spc_DIM_PRESCRIBER_AMA_OUTPUT_FLAG_UPPDATE]	 
(
	@P_RUN_CTRL_ID   int
)
AS

BEGIN	
	
	SET NOCOUNT ON;
    
	-- base data from STG_TORRO_PRESCRIBER

	SELECT distinct PRESCRIBERCODE, USEPRESCRIBERMASK INTO #WRK_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD 
	FROM EDWSTG.STG_TORRO_PRESCRIBER WHERE USEPRESCRIBERMASK <> 0

	/* ama new changes after discussion with penny and mark on 0119 */
	
	SELECT DIM_MASTER_PRESCRIBER_KEY, 0 AS AMA_OPTOUT_FLAG_KEY into #WRK_DIM_MASTER_PRESCRIBER_UPD1_UPD0
	FROM EDW.DIM_MASTER_PRESCRIBER DMP WHERE AMA_OPTOUT_FLAG_KEY = 1
	AND NOT EXISTS (
	SELECT 1 
	FROM EDWSTG.STG_TORRO_PRESCRIBER WHERE USEPRESCRIBERMASK = 1 
	AND PRESCRIBERCODE = DMP.MASTER_PRESCRIBER_CODE )
	

	
	IF EXISTS(SELECT TOP 1 * FROM #WRK_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD)
	BEGIN
	CREATE CLUSTERED INDEX CDX_PRESCRIBERCODE ON #WRK_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD
	(PRESCRIBERCODE ASC)
		-- 6 SEC
		
		SELECT DIM_MASTER_PRESCRIBER_KEY , [AMA_OPTOUT_FLAG_KEY], 
		GETDATE() AS UPDATE_DTT, ROW_NUMBER() OVER (ORDER BY DIM_MASTER_PRESCRIBER_KEY ASC) AS ROW_NBR
		INTO #L_TEMP_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD
		FROM
		(SELECT DIM_MASTER_PRESCRIBER_KEY ,WRK.USEPRESCRIBERMASK AS [AMA_OPTOUT_FLAG_KEY]
		FROM #WRK_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD AS WRK
		JOIN EDW.DIM_MASTER_PRESCRIBER DMP (NOLOCK) ON DMP.MASTER_PRESCRIBER_CODE = WRK.PRESCRIBERCODE  
			WHERE DMP.AMA_OPTOUT_FLAG_KEY <> WRK.USEPRESCRIBERMASK
		UNION
		SELECT DIM_MASTER_PRESCRIBER_KEY, [AMA_OPTOUT_FLAG_KEY] FROM #WRK_DIM_MASTER_PRESCRIBER_UPD1_UPD0
		) A
		-- 10 SEC TO LOAD 14 MILL

	CREATE CLUSTERED INDEX CIDX_WRK_PRESCRIBER_ID_LIST_KEY ON #L_TEMP_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD( ROW_NBR ASC, DIM_MASTER_PRESCRIBER_KEY ASC)

	END

	------------------------------
	DECLARE
		@ROW_COUNT INTEGER = 0
	,	@LOOP INTEGER = 0
	,	@UPD_COUNT INTEGER
	,	@ROW_CNT INTEGER
	,	@ROW_CNT1 INTEGER

	IF (OBJECT_ID('TEMPDB..#L_TEMP_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD') ) IS NOT NULL AND EXISTS(SELECT TOP 1 * FROM #L_TEMP_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD)
	BEGIN
		SELECT @ROW_COUNT = COUNT(1) FROM #L_TEMP_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD
	END

	--PRINT 'TOTAL ROWS TO BE UPDATED: ' + CAST(@ROW_COUNT AS VARCHAR(MAX))

	SET @ROW_CNT = 0
	SET @ROW_CNT1 = 1000000

 
	WHILE (@LOOP <= @ROW_COUNT / 1000000 AND @ROW_COUNT > 0 )
	BEGIN

		BEGIN TRANSACTION DP1000000

			UPDATE DMP 
			SET
				DMP.AMA_OPTOUT_FLAG_KEY = WRK.AMA_OPTOUT_FLAG_KEY
			,	DMP.UPDATE_DTT = WRK.UPDATE_DTT
			,	DMP.RUN_CTRL_ID = @P_RUN_CTRL_ID
			FROM EDW.DIM_MASTER_PRESCRIBER DMP
			JOIN #L_TEMP_DIM_PRESCRIBER_AMA_OPTOUT_FLAG_UPD WRK ON WRK.DIM_MASTER_PRESCRIBER_KEY = DMP.DIM_MASTER_PRESCRIBER_KEY
			WHERE
				WRK.ROW_NBR >= @ROW_CNT 
			AND	WRK.ROW_NBR < @ROW_CNT1

			SET @UPD_COUNT = @@ROWCOUNT

		COMMIT TRANSACTION DP1000000

		SET @ROW_CNT = @ROW_CNT + 1000000
		SET @ROW_CNT1 = @ROW_CNT1 + 1000000
		SET @LOOP = @LOOP + 1

	END

	-- total update took 3 mins and 4 sec;

 END

GO


